dynamic_props = { element look ->
  <-(if (is (type state) "string" ){
    <- (list 0 0 "")
  }{
    xy = (get state APP_XY)
    props = (get element 0)

    x = (add (get xy 0) (get props 0))
    y = (add (get xy 1) (get props 1))

    <- (if (or
      (less_than (get (get state APP_XY) 0) 0)
      (less_than (get (get state APP_XY) 1) 0)
    ) {
      <- (list x y "")
    } {
      <- (if (is look void) {
        <- (list x y (get (get element 0) 2)) 
      } {
        <- (list x y look)
      })
    })
  })
}

app_width = 80
app_height = 24

centered_modal = ({
  props = (list 0 0 "")

  on_state = {
    (if (or
      (less_than (get (get state APP_XY) 0) 0)
      (less_than (get (get state APP_XY) 1) 0)
    ) {
      mapped = (map (range (subtract (rows) 0)) { _ index -> 
        <- (if (is index (integer (divide (rows) 2))) {
          r = (map (range (subtract (columns) 0)) { _ _ ->
            <- " "
          })
          point = (subtract (integer (divide (columns) 2)) 12)
          point = (if (less_than point 0) {
            <- 0
          }{
            <- point
          })
          <- (set r "Enlarge Terminal Window" point)
        } {
          <- (map (range (subtract (columns) 0)) { _ _ ->
            <- " "
          })
        })
      })

      (draw (list 0 0 mapped))
    } {
      (draw (list 0 0 ""))
    })

    <- state
  }

  <- (list void (list on_state void void void void))
})

screen = ({
  props = (list 
    0
    0
    "+"
  )
  
  on_state = {
    (draw (dynamic_props screen void))
    <- state
  }

  on_loop = {
    x = (integer (divide (subtract (columns) app_width) 2)) // center position on x
    y = (integer (divide (subtract (rows) app_height) 2))// center position on y

    curr = (list x y)

    <- (if (is curr (get state APP_XY)){
      <- state
    } {
      (print "\e[2;0H\e[0J")

      <- (set state (list x y) APP_XY)
    })
  }

  <- (list props (list on_state on_loop void void void))
})

title = ({
  on_state = {
    look = (reduce (range (columns)) {accum _ _ -> <- (join accum " ") } "")
    look = (set look (join "Crumbicon: " file)  0) 
    look = (inverse look)

    (draw (list 0 0 look))
    <- state
  }

  <- (list void (list on_state void void void void))
})

canvas = ({
  props = (list
    22 // (integer (divide (subtract (columns) 32) 2)) // center position on x
    2 //(subtract (integer (divide (subtract (rows) 16) 2)) 6)// center position on y and up
    (map (range 16) { _ _ -> 
      <- (map (range 32) { _ _ -> <-  ":"})
    })
  )

  on_state = {
    data = (get state DATA)

    mapped = (map (range 16) { _ row -> 
      <- (map (range 32) { _ col -> 
        halfcol = (integer (divide col 2))
        <- (if (is (get data (add (multiply row 16) halfcol)) void) {
          <- ":"
        } {
          <- (background (get data (add (multiply row 16) halfcol)) " ")
        })
      })
    })

    (draw (dynamic_props canvas mapped))
    <- state
  }

  on_click = {
    props = (dynamic_props canvas (get (get canvas 0) 2) )

    <- (if (is (mouse_over props mouse_xy) 1) {
      r = (subtract (get mouse_xy 1) (get props 1))
      c = (integer (divide (subtract (integer (subtract (add (get mouse_xy 0) 1 ) (get props 0) )) 1) 2))
      
      data = (set (get state DATA) (get state PAINTBRUSH) (add (multiply r 16) c))

      <- (set state data DATA)
    } {
      <- state
    })
  }

  <- (list props (list on_state void void void on_click))
})

color_picker = ({
  props = (list
    3
    2
    (map (range 18) { item row -> 
      <- (map (range 12) { item col -> <- (background (add (multiply row 12) col 16) " ")})
    })
  )

  on_state = {
    (draw (dynamic_props color_picker void))
    <- state
  }

  on_click = {
    props = (dynamic_props color_picker void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      // calculate color
      r = (subtract (get mouse_xy 1) (get props 1))
      c = (subtract (integer (subtract (add (get mouse_xy 0) 1 ) (get props 0) )) 1)
      calculated = (add (add (multiply r 12) c) 16)

      pl = (get (get state PALETTE) 0)
      palette = (set (get (get state PALETTE) 1) calculated pl)
      state = (set state (list pl palette) PALETTE)

      <- (set state calculated PAINTBRUSH)
    } {
      <- state
    })
  }
  <- (list props (list on_state void void void on_click))
})

gray_picker = ({
  props = (list
    16 //(subtract (integer (divide (subtract (columns) 3) 2)) 20) // center position on x and left
    2 //(add (integer (divide (subtract (rows) 16) 2)) 11)// center position on y and down
    (map (range 12) { item row -> 
      <- (map (range 2) { item col -> <- (background (add (multiply row 2) col 232) " ")})
    })
  )

  on_state = {
    (draw (dynamic_props gray_picker void))
    <- state
  }

  on_click = {
      props = (dynamic_props gray_picker void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      // calculate color
      r = (subtract (get mouse_xy 1) (get props 1))
      c = (subtract (integer (subtract (add (get mouse_xy 0) 1 ) (get props 0) )) 1)
      calculated = (add (add (multiply r 2) c) 232)

      pl = (get (get state PALETTE) 0)
      palette = (set (get (get state PALETTE) 1) calculated pl)
      state = (set state (list pl palette) PALETTE)

      <- (set state calculated PAINTBRUSH)
    } {
      <- state
    })
  }

  <- (list props (list on_state void void void on_click))
})

sixteen_picker = ({
  props = (list
    1 // (add (integer (divide (subtract (columns) 1) 2)) 20) // center position on x and left
    2 // (add (integer (divide (subtract (rows) 16) 2)) 11)// center position on y and down
    (map (range 16) { item row -> 
      <- (map (range 1) { item col -> <- 
        (background 
          (rgb_to_ansi_8 (ansi_8_to_rgb (add (multiply row 1) col)))
       " ")})
    })
  )

  on_state = {
    (draw (dynamic_props sixteen_picker void))
    <- state
  }

  on_click = {
      props = (dynamic_props sixteen_picker void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      // calculate color
      r = (subtract (get mouse_xy 1) (get props 1)) // (get (get component 1) 1) == y
      c = (subtract (integer (subtract (add (get mouse_xy 0) 1 ) (get props 0) )) 1) // (get (get component 1) 0) == x
      calculated = (rgb_to_ansi_8 (ansi_8_to_rgb (add (multiply r 1) c) )) 

      pl = (get (get state PALETTE) 0)
      palette = (set (get (get state PALETTE) 1) calculated pl)
      state = (set state (list pl palette) PALETTE)

      <- (set state calculated PAINTBRUSH)
    } {
      <- state
    })
  }
  <- (list props (list on_state void void void on_click))
})

empty_picker = ({
  props = (list
    16
    19
    (list 
      (list ":" ":")
    )
  )

  on_state = {
    (draw (dynamic_props empty_picker void))
    <- state
  }

  on_click = {
      props = (dynamic_props empty_picker void)

    <- (if (is (mouse_over props mouse_xy) 1) {

      pl = (get (get state PALETTE) 0)
      palette = (set (get (get state PALETTE) 1) void pl)
      state = (set state (list pl palette) PALETTE)

      <- (set state void PAINTBRUSH)
    } {
      <- state
    })
  }

  <- (list props (list on_state void void void on_click))
})

color_selected = ({
  props = (list 
    20
    2
    (list " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " " ")
  )

  on_state = {
    mapped = (if (is (get state PAINTBRUSH) void) {
      <- (get (get color_selected 0) 2)
    }{
      <- (background (get state PAINTBRUSH) (get (get color_selected 0) 2))
    })

    (draw (dynamic_props color_selected mapped))
    <-state
  }

  <- (list props (list on_state void void void void))
})

palette = ({
  props = (list
    22
    19 
    (list
      (map (range 32) {_ index -> 
        <- (if (or (is (remainder index 4) 1) (is (remainder index 4) 2)) {<- ":"} {<- " "})
      })
    )
  )

  on_state = {
    pb = (get state PAINTBRUSH)
    pl = (get (get state PALETTE) 0)
    pll = (get (get state PALETTE) 1)

    look = (get (get palette 0) 2)

    mapped = (map look { item row ->
        <- (map item { item col ->col
          <- (if (or (is (remainder col 4) 1) (is (remainder col 4) 2)) {
            coloring =  (get pll (integer (divide col 4)))
            active = (or 
              (is (add (multiply pl 4) 2) col)
              (is (add (multiply pl 4) 1) col)
            )

            <- (if (is (type coloring) "integer") {
              <- (background coloring (if (is active 1) {<- "+"} {<- " "} ))
            } {
              <- (if (is active 1) {<- "+"} {<- item} )
            })

            <- item
          } {
            <- " "
          })

        })
      })


    (draw (dynamic_props palette mapped))
    <- state
  }

  on_click = {
    props = (dynamic_props palette void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      // calculate color
      r = (subtract (get mouse_xy 1) (get props 1))
      c = (subtract (integer (subtract (add (get mouse_xy 0) 1 ) (get props 0) )) 1)
      
      selected = (if (or (is (remainder c 4) 1) (is (remainder c 4) 2)) {
        <- (integer (divide c 4))
      } {
        <- (get (get state PALETTE) 0)
      })
      state = (set state (set (get state PALETTE) selected 0) PALETTE)

      pll = (get (get state PALETTE) 1)
      pb = (get pll selected)
      state = (set state pb PAINTBRUSH)

      <- state
    } {
      <- state
    })
  }

  <- (list props (list on_state void void void on_click))
})

button_save = ({
  props = (list
    55
    2
    (list
      (list "╔" "═" "═" "═" "═" "═" "═" "╗")
      (list "║" " " "S" "a" "v" "e" " " "║")
      (list "╚" "═" "═" "═" "═" "═" "═" "╝")
    )
  )

  on_state = {
    (draw (dynamic_props button_save void))
    <- state
  }

  on_click = {
    props = (dynamic_props button_save void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      <- void
    } {
      <- state
    })
  }

  <- (list props (list on_state void void void on_click))
})

button_wipe = ({
  props = (list
    55
    15
    (list
      (list "╔" "═" "═" "═" "═" "═" "═" "╗")
      (list "║" " " "W" "i" "p" "e" " " "║")
      (list "╚" "═" "═" "═" "═" "═" "═" "╝")
    )
  )

  on_state = {
    (draw (dynamic_props button_wipe void))
    <- state
  }

  on_click = {
    props = (dynamic_props button_wipe void)

    <- (if (is (mouse_over props mouse_xy) 1) {
      // reset canvas data
      <- (set state (map (range 256) { _ _ -> <- void}) DATA)
    } {
      <- state
    })
  }
  <- (list props (list on_state void void void on_click))
})

elements = (list screen centered_modal title canvas color_picker gray_picker empty_picker sixteen_picker palette color_selected button_save button_wipe )
