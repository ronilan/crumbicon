svg = { data props palette target ->

  data_to_string = { data ->
    <- (reduce data { accum item index -> 
      <- (join accum ":" (if (is item void) {
        <- "void"
      } {
        <- (string item) 
      })) 
    } "")
  }

  (clear_screen)
  (colored_splash_msg 10 (join "Exporting " target))

  num_rows = (get props 0)
  num_columns = (get props 1)
  pixel_width = (get props 2)

  open = (join 
    "<svg version=\"1.1\" baseProfile=\"full\"  width=\""
    (string(multiply num_columns pixel_width))
    "\" height=\""
    (string(multiply num_rows pixel_width))
    "\" xmlns=\"http://www.w3.org/2000/svg\">"
  )
  close = "</svg>"

  rendered = (reduce  
    (map data { item index ->
      <- (join 
        "<rect x=\""
        (string (multiply (remainder index num_columns) pixel_width))
        "\" y=\""
        (string (multiply (integer (divide index num_columns)) pixel_width))
        "\" width=\""
        (string pixel_width)
        "\" height=\""
        (string pixel_width)
        "\" fill=\"" 
        (if (is item void) {<- "none"} {<- (join "#" (ansi_8_to_hex item))})
        "\" />\n"
        )
    }) { accum item index -> 
      <- (join accum item) 
  } "")

  saved_data = (data_to_string data)
  saved_palette = (data_to_string palette)

  str = (join open "\n" rendered "\n" close "\n" "<!-- crumbicon-data" saved_data "crumbicon-data --><!-- crumbicon-palette" saved_palette "crumbicon-palette -->")

  (write_file target str)

  (clear_screen)
  (colored_splash_msg 10 (join "Saved " target))
  (wait 0.5)
}
