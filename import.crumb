crumbicon = { path_to_file ->
  // utility
  // split a string on a single char separator
  split = {in sep ->
    // convert to list of chars
    arr = (map (range (length in)) {index _ -> <- (get in index)})
    // reduce list of chars to list of words
    <- (if (is sep "") {<- arr} {
      <- (reduce arr {acc char _ ->
        last_index = (subtract (length acc) 1)

        <- (if (is char sep) {
          // add new item to result if we come across a separator
          <- (insert acc "") 
        } {
          // else, add char to the last item
          <- (set acc (insert (get acc last_index) char) last_index)
        })
      } (list ""))
    })
  }


  data_from_file = { text start_needle end_needle alt ->
    <- (if (is text void) {
      <- alt // can't open file or file does not exist
    } {
      start = (find text start_needle)
      end = (find text end_needle)
      <- (if (or (is start void) (is end void)) {
        <- void // the file exist but no valid crumbicon data found
      } {
        start = (add start (add (length start_needle) 1))
        <- (map (split (get text start end) ":") {item _ -> 
          <- (if (is item "void") {
            <- void
          } {
            <- (integer item)
          })
        })
      })
    })
  }

  text = (read_file path_to_file)

  crumbicon-data = (data_from_file text "<!-- crumbicon-data" "crumbicon-data -->" (map (range 256) { _ _ -> <- void}))
  crumbicon-palette = (data_from_file text "<!-- crumbicon-palette" "crumbicon-palette -->" (map (range 256) { _ _ -> <- void}))

  <- (if (or (is crumbicon-data void) (is crumbicon-palette void)) {
    <- void
  } {
    <- (list crumbicon-data crumbicon-palette)
  })
}
