(use "./usables/colors.crumb" {

  file_name = (if (is (length arguments) 2) {
    <- (get arguments 1)
  } {
    <- "favicon.svg"
  })

  from_file = (use "./src/import.crumb" "./usables/tui.crumb" "./src/splash.crumb" {
    <- (crumbicon file_name)
  })

  (if (not (is from_file void)) {
    // state access constants
    APP_XY = 0
    PAINTBRUSH = 1
    DATA = 2
    PALETTE = 3
    SIZE = 4
    SAVE = 5

    //state after user manipulation
    state = (use "./usables/tui.crumb" "./src/elements.crumb" "./src/splash.crumb" {

      x = (integer (divide (subtract (columns) app_width) 2)) // center position on x
      y = (integer (divide (subtract (rows) app_height) 2)) // center position on y

      // initial state
      state = (list
        (list x y) // screen anchor
        void // paintbrush color
        (get from_file 0) // canvas data
        (list 0 (get from_file 1)) // palette
        (get from_file 2) // size
        1 // save or abort?
      )

      <- (start state elements)
    })

    (if (is (get state SAVE) 1) {
      (use "./src/export.crumb" "./usables/tui.crumb" "./src/splash.crumb" {
        (svg (get state DATA) (list 16 16 30) (get (get state PALETTE) 1) file_name)
      })
    })
  })

})
