// utility

// split a string on a single char separator
split = {in sep ->
  // convert to list of chars
  arr = (map (range (length in)) {index _ -> <- (get in index)})
  // reduce list of chars to list of words
  <- (if (is sep "") {<- arr} {
    <- (reduce arr {acc char _ ->
      last_index = (subtract (length acc) 1)

      <- (if (is char sep) {
        // add new item to result if we come across a separator
        <- (insert acc "") 
      } {
        // else, add char to the last item
        <- (set acc (insert (get acc last_index) char) last_index)
      })
    } (list ""))
  })
}

data_from_file = { text start_needle end_needle alt ->
  <- (if (is text void) {
    <- alt
  } {
    start = (find text start_needle)
    end = (find text end_needle)
    <- (if (or (is start void) (is end void)) {
      <- alt
    } {
      <- (if (is (type start) integer) {
        <- alt
      } {
        start = (add start (add (length start_needle) 1))
        arr = (map (split (get text start end) ":") {item _ -> 
          <- (if (is item "void") {
            <- void
          } {
            <- (integer item)
          })
        })
        <- arr
      })
    })
  })
}

file = (if (is (length arguments) 2) {<- (get arguments 1)} {<- "favicon.svg"})
text = (read_file file)

crumbicon-data = (data_from_file text "<!-- crumbicon-data" "crumbicon-data -->" (map (range 256) { _ _ -> <- void}))
crumbicon-palette = (data_from_file text "<!-- crumbicon-palette" "crumbicon-palette -->" (map (range 256) { _ _ -> <- void}))

//state after user manipulation
state = (use "./usables/colors.crumb" {
  <- (use "./usables/tui.crumb" "./elements.crumb" {

    x = (integer (divide (subtract (columns) app_width) 2)) // center position on x
    y = (integer (divide (subtract (rows) app_height) 2)) // center position on y

    PAINTBRUSH = 0
    DATA = 1
    APP_XY = 2
    PALETTE = 3

    // initial state
    state = (list
      void // paintbrush color
      crumbicon-data // canvas data
      (list x y) // screen anchor
      (list 0 crumbicon-palette) // palette
    )

    <- (start state elements)
  })
})

(use "export.crumb" {
  (svg (get state 1) (list 16 16 30) (get (get state 3) 1) file)
})
